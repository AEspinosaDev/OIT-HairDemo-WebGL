<!DOCTYPE html>
<!--
 ██╗    ██╗███████╗██╗ ██████╗ ██╗  ██╗████████╗███████╗██████╗     ██████╗ ██╗     ███████╗███╗   ██╗██████╗ ███████╗██████╗ 
██║    ██║██╔════╝██║██╔════╝ ██║  ██║╚══██╔══╝██╔════╝██╔══██╗    ██╔══██╗██║     ██╔════╝████╗  ██║██╔══██╗██╔════╝██╔══██╗
██║ █╗ ██║█████╗  ██║██║  ███╗███████║   ██║   █████╗  ██║  ██║    ██████╔╝██║     █████╗  ██╔██╗ ██║██║  ██║█████╗  ██║  ██║
██║███╗██║██╔══╝  ██║██║   ██║██╔══██║   ██║   ██╔══╝  ██║  ██║    ██╔══██╗██║     ██╔══╝  ██║╚██╗██║██║  ██║██╔══╝  ██║  ██║
╚███╔███╔╝███████╗██║╚██████╔╝██║  ██║   ██║   ███████╗██████╔╝    ██████╔╝███████╗███████╗██║ ╚████║██████╔╝███████╗██████╔╝
 ╚══╝╚══╝ ╚══════╝╚═╝ ╚═════╝ ╚═╝  ╚═╝   ╚═╝   ╚══════╝╚═════╝     ╚═════╝ ╚══════╝╚══════╝╚═╝  ╚═══╝╚═════╝ ╚══════╝╚═════╝ 

MADE BY
                                                         
 ▄▀█ █▄░█ ▀█▀ █▀█ █▄░█ █ █▀█ █▀▀ █▀ █▀█ █ █▄░█ █▀█ █▀ ▄▀█
 █▀█ █░▀█ ░█░ █▄█ █░▀█ █ █▄█ ██▄ ▄█ █▀▀ █ █░▀█ █▄█ ▄█ █▀█
-->
<!-- 
    Based on "Weighted Blended Order-Independent Transparency"
    By Morgan McGuire and Louis Bavoil 
    http://jcgt.org/published/0002/02/09/
-->
<html>
  <head>
    <title>Weighted OIT for HAIR</title>
    <meta charset="utf-8" />
    <script src="dependencies/gl-matrix.js"></script>
    <script src="node_modules/webgl-obj-loader/dist/webgl-obj-loader.js"></script>
    <link rel="stylesheet" href="css/style.css" />
  </head>

  <body>
    <canvas id="gl-canvas"></canvas>

    <!--
    ▄▀█ █▀▀ █▀▀ █░█ █▀▄▀█ █░█ █░░ ▄▀█ ▀█▀ █ █▀█ █▄░█
    █▀█ █▄▄ █▄▄ █▄█ █░▀░█ █▄█ █▄▄ █▀█ ░█░ █ █▄█ █░▀█
    -->
    <script type="x-shader/x-vertex" id="vertex-accum">
      #version 300 es

      layout(location=0) in vec3 position;
      layout(location=1) in vec2 uv;
      layout(location=2) in vec3 normal;

      layout(std140, column_major) uniform;
      uniform SceneUniforms {
          mat4 uProj;
          mat4 uView;
          mat4 uModelView;
          vec4 uEyePosition;
          vec4 uLightPosition;
      };

      out vec3 vPosition;
      out vec2 vUV;
      out vec3 vNormal;
      out vec3 vLightPos;
      flat out vec4 vColor;

      void main() {

          vPosition = (uModelView * vec4(position,1.0)).xyz;
          vLightPos = (uView * vec4(uLightPosition.xyz,1.0)).xyz;
          vUV = uv;
          vNormal = mat3(transpose(inverse(uModelView))) * normal;

          vColor = vec4(0.38*2.5,0.22*2.5,0.2*2.5,0.7);

          gl_Position =  uProj* uModelView *vec4(position,1.0);

      }
    </script>
    <script type="x-shader/x-fragment" id="fragment-accum">
      #version 300 es
      precision highp float;

      layout(std140, column_major) uniform;
      uniform SceneUniforms {
          mat4 uProj;
          mat4 uView;
          mat4 uModelView;
          vec4 uEyePosition;
          vec4 uLightPosition;
      };

      uniform sampler2D uTexture;

      in vec3 vPosition;
      in vec2 vUV;
      in vec3 vNormal;
      flat in vec4 vColor;
      in vec3 vLightPos;

      layout(location=0) out vec4 accumColor;
      layout(location=1) out float accumAlpha;



      float weight(float z, float a) {
          //return clamp(pow(min(1.0, a * 10.0) + 0.01, 3.0) * 1e8 * pow(1.0 - z * 0.9, 3.0), 1e-2, 3e3);
          return a*max(pow(10.0,-2.0),min(3000.0,0.03/(pow(10.0,-5.0)+pow((abs(z)/200.0),4.0)))); //EQUATION 9 ORIGINAL PAPER. GOOD FOR MIDDLE DISTANCES

      }

      void main() {
          vec3 position = vPosition.xyz;
          vec3 normal = normalize(vNormal.xyz);
          vec2 uv = vUV;

          //ALPHA TEST
          //if(texture(uTexture, uv).r<0.1)discard;

          //aqui premultiplica
          vec4 baseColor = vColor * texture(uTexture, uv);
          vec3 eyeDirection = normalize(uEyePosition.xyz - position);
          vec3 lightVec = vLightPos.xyz - position;
          vec3 lightDirection = normalize(lightVec);
          vec3 reflectionDirection = reflect(-lightDirection, normal);
          float nDotL = max(dot(lightDirection, normal), 0.0);
          float diffuse = nDotL;
          float ambient = 0.2;
          float specular = pow(max(dot(reflectionDirection, eyeDirection), 0.0), 20.0);

          vec4 color = vec4((ambient + diffuse + specular) * baseColor.rgb, vColor.a);
          color.rgb *= color.a;

          float w = weight(gl_FragCoord.z, color.a);
          accumColor = vec4(color.rgb * w, color.a);

          accumAlpha = color.a * w;
      }
    </script>


    <!--
    █▀▀ █▀█ █▀▄▀█ █▀█ █▀█ █▀ █ ▀█▀ █ █▀█ █▄░█
    █▄▄ █▄█ █░▀░█ █▀▀ █▄█ ▄█ █ ░█░ █ █▄█ █░▀█
    -->
    <script type="x-shader/x-fragment" id="fragment-draw">
      #version 300 es
      precision highp float;

      uniform sampler2D uAccumulate;
      uniform sampler2D uAccumulateAlpha;
      out vec4 fragColor;
      void main() {
          ivec2 fragCoord = ivec2(gl_FragCoord.xy);
          vec4 accum = texelFetch(uAccumulate, fragCoord, 0);
          float a = 1.0 - accum.a;
          accum.a = texelFetch(uAccumulateAlpha, fragCoord, 0).r;
          fragColor = vec4(a * accum.rgb / clamp(accum.a, 0.001, 50000.0), a);
      }
    </script>


    <!--
    █▀█ █▀█ ▄▀█ █▀█ █░█ █▀▀
    █▄█ █▀▀ █▀█ ▀▀█ █▄█ ██▄ 
    -->
    
    <script type="x-shader/x-vertex" id="head_vert">
      #version 300 es

      layout(location=0) in vec3 position;
      layout(location=1) in vec2 uv;
      layout(location=2) in vec3 normal;

      layout(std140, column_major) uniform;

      uniform SceneUniforms {
          mat4 uProj;
          mat4 uView;
          mat4 uModelView;
          vec4 uEyePosition;
          vec4 uLightPosition;
      };

      out vec3 vPosition;
      out vec2 vUV;
      out vec3 vNormal;
      out vec3 vLightPos;
      flat out vec4 vColor;

      void main() {
          vec4 pos = uModelView * vec4(position,1.0);
          vLightPos = (uView * vec4(uLightPosition.xyz,1.0)).xyz;
          vPosition = pos.xyz;
          vUV = uv;
          vNormal = mat3(transpose(inverse(uModelView))) * normal;
          vColor = vec4(0.97,0.86,0.78,1.0);
          gl_Position =  uProj* uModelView *vec4(position,1.0);
      }
    </script>
    <script type="x-shader/x-fragment" id="head_frag">
      #version 300 es
      precision highp float;

      layout(std140, column_major) uniform;
      uniform SceneUniforms {
          mat4 uProj;
          mat4 uView;
          mat4 uModelView;
          vec4 uEyePosition;
          vec4 uLightPosition;
      };
      uniform sampler2D uTexture;

      in vec3 vPosition;
      in vec2 vUV;
      in vec3 vNormal;
      in vec3 vLightPos;
      flat in vec4 vColor;

      out vec4 fragColor;

      float gloss = 20.0;

      void main() {
          vec3 c = vec3(0.0);
          vec3 position = vPosition.xyz;
          vec3 normal = normalize(vNormal.xyz);

          float ambient = 0.2;
          vec3 eyeDirection = normalize(- position);
          vec3 lightVec = normalize(vLightPos - position);
          float diffuse = dot(normal,lightVec);
          float spec = pow(max(dot(reflect(-lightVec,normal),eyeDirection), 0.0), 10.0);

          c = vColor.rgb * (ambient + diffuse+ spec);
          fragColor = vec4(c,1.0);
      }
    </script>

    <script type="x-shader/x-vertex" id="vertex-quad">
      #version 300 es

      layout(location=0) in vec4 aPosition;

      void main() {
          gl_Position = aPosition;
      }
    </script>
    <script type="x-shader/x-fragment" id="screen-draw">
      #version 300 es
      precision highp float;


      uniform sampler2D screen;

      out vec4 fragColor;
      void main() {
          ivec2 fragCoord = ivec2(gl_FragCoord.xy);
          fragColor = vec4(texelFetch(screen, fragCoord,0).rgb, 1.0f);
      }
    </script>

    <script type="module" src="main.js"></script>
  </body>
</html>
